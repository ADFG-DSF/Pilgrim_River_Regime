
  model {
    for (y in 1:nyrs) {
        R[y] ~ dlnorm(mu[y], tau[lambda[y]])
        fit[y] <- log(S[y]) + lnalpha[lambda[y]] - (beta[lambda[y]] * S[y]/10^d)
        e[y] <- log(R[y]) - fit[y]
        lambda[y] ~ dcat(gamma[y,1:2])
        lambda_1[y] <- lambda[y] - 1
    }
    mu[1] <- fit[1] + ar1 * phi * e0
    for (y in 2:nyrs) {
        mu[y] <- fit[y] + ar1 * phi * e[y - 1]
        for(r in 1:2){
          gamma[y, r] <- pi[lambda[y - 1], r]
        }
    }
    for (r in 1:2){
    #   lnalpha[r] ~ dunif(0, 5)
    #   beta[r] ~ dunif(0, 5)
    #  sigma[r] ~ dgamma(5, 15)
     tau[r] <- 1/(sigma[r] * sigma[r])
    }
    lnalpha[1] ~ dnorm(0, 1)T(lnalpha[2], )
    lnalpha[2] ~ dnorm(0, 1)T(0, )
    beta[1] ~ dnorm(0, 1)T(0, )
    beta[2] ~ dnorm(0, 1)T(beta[1], )
    sigma[1] ~ dgamma(5, 15)
    sigma[2] ~ dnorm(0, 1)T(sigma[1],)
    gamma[1, 1] <- 13/17
    gamma[1, 2] <- 4/17
    pi[1,1] <- p_1[1]
    pi[1,2] <- p_1[2]
    pi[2,1] <- p_2[1]
    pi[2,2] <- p_2[2]
    p_1[1:2] ~ ddirch(c(13, 2))
    p_2[1:2] ~ ddirch(c(2, 4))
    phi ~ dnorm(0, 4)T(-1, 1)
    e0 ~ dnorm(0, 0.001)
  }
      